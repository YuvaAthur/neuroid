// $Id$
/**
 * For running the network a number of times (say 100) to get
 * statistically significant performance results.
 */

testBoost() {
    void run() {
	init();

	startTime = System.currentTimeMillis();

	for (super.nuBoost = fromBoost; super.nuBoost < toBoost;
	     super.nuBoost += 2 ) {

	    meanQc = 0; minQc = Double.MAX_VALUE; maxQc = Double.MIN_VALUE;
	    meanQs = 0; minQs = Double.MAX_VALUE; maxQs = Double.MIN_VALUE;
	    meanRc = 0; minRc = Double.MAX_VALUE; maxRc = Double.MIN_VALUE;

	    for (numberOfTrials = 0; numberOfTrials < maxTrials; numberOfTrials++) {
		runOnce(super);
		meanQc += qc;
		meanQs += qs;
		meanRc += rc;
		minQc = Math.min(minQc, qc);
		minQs = Math.min(minQs, qs);
		minRc = Math.min(minRc, rc);
		maxQc = Math.max(maxQc, qc);
		maxQs = Math.max(maxQs, qs);
		maxRc = Math.max(maxRc, rc);
	    }
	    meanQc /= maxTrials;
	    meanQs /= maxTrials;
	    meanRc /= maxTrials;

	    correctProfile.put(new Double(super.nuBoost),
			       new ErrorValue(meanQc, minQc, maxQc));
	    spuriousProfile.put(new Double(super.nuBoost),
				new ErrorValue(meanQs, minQs, maxQs));
	    correctRatioProfile.put(new Double(super.nuBoost),
				    new ErrorValue(meanRc, minRc, maxRc));
     
	} // end of for (nuBoost = 1; nuBoost < 5; nuBoost++)

	print("Elapsed experiment time: " +
	      ((double)System.currentTimeMillis() - startTime)/60000 +
	      " mins");

	save(super, idString + ".data");
    }

    //datas = new Vector(combinedProfilePlot(correctProfile, spuriousProfile, idString));

    // Called by combinedProfilePlot
    void createPlot(java.util.List datas) {

	data = new Grapher.VerticalBar(nsh.grapher, "Empirical factor",
				       calcBoost, new Range(0, 1));
	datas.add(data);

	datasPlot(datas, idString, "Amplification Factor \\nu", "Quality",
		  "Performance with " + numberOfMedialAreas + " Layers, " +
		  numberOfObjects + " Objects over " +
		  maxTrials + " Trials" );

    }

    // Runs everything
    void runAll() {
	run();
	combinedProfilePlot(super);
    }

    void batch() {
	startTime = System.currentTimeMillis();

	for (int i = 0; i < 3; i++) {
	    super.numberOfMedialAreas = 2 + i;
	    super.maxTrials = 10;
	    run();
	}

	print("Total experiment time: " +
	      (System.currentTimeMillis() - startTime)/60000 + " mins");
    }

    void init() {
	super.correctProfile = new Profile();
	super.spuriousProfile = new Profile();
	super.correctRatioProfile = new Profile();

	super.gamma = (numberOfMedialAreas - 1) * timeConstantS;
	super.timeConstantM = getTimeConstantM(super);
	super.threshold = 1.5 * getMaxEPSP(super); // This calculation is tainted, may cause wrong results (This should be adaptively adjusted)
	super.segregation = getSegregation(super);

	super.idString =
	    "boost-" + fromBoost + "to" + toBoost + "-"
	    + maxTrials + "trials-" + numberOfObjects + "objects-" +
	    segregation + "phi-" + timeConstantM + "taum-" 
	    + numberOfMedialAreas + "layers";

    }

    // Save me, save you
    nsh.boost = this;

    setAccessibility(true);

    maxTrials = 2; //10
    numberOfMedialAreas = 2;
    numberOfObjects = 5;
    timeConstantS = 7;
    delay = 3;

    fromBoost = 1;
    toBoost = 10;
    dataPoints = 7;

    calcBoost = 6;


}
